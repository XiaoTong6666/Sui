import org.apache.tools.ant.filters.FixCrLfFilter
import java.security.MessageDigest
import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id('com.android.application')
    id('dev.rikka.tools.refine')
}

evaluationDependsOn(':standalone')

apply from: file(rootProject.file('module.gradle'))

android {
    namespace "rikka.sui"
    defaultConfig {
        versionCode gitCommitCount
        versionName moduleVersion.substring(1)
        externalNativeBuild {
            cmake {
                arguments "-DZYGISK_MODULE_ID:STRING=\"$moduleId\"",
                        "-DANDROID_STL=none"
            }
        }
    }

    flavorDimensions "mode"
    productFlavors {
        zygisk { dimension "mode" }
        standalone {
            dimension "mode"
            externalNativeBuild {
                cmake {
                    // [关键修正] Standalone 模式下，模块 ID 是 "sui"
                    // 必须覆盖之前的默认值，否则 C++ 代码会去错误的路径找 dex
                    arguments "-DZYGISK_MODULE_ID:STRING=\"sui\""
                }
            }
        }
    }

    buildFeatures {
        viewBinding true
        prefab true
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.31.0+"
        }
    }
    buildTypes {
        debug {
            defaultConfig.minSdk 24
            multiDexEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        checkReleaseBuilds false
    }
    dependenciesInfo {
        includeInApk false
    }
}

dependencies {
    implementation libs.refine.runtime

    implementation 'org.lsposed.libcxx:libcxx:29.0.14206865'
    implementation 'dev.rikka.ndk.thirdparty:nativehelper:1.0.1'
    implementation 'dev.rikka.rikkax.parcelablelist:parcelablelist:2.0.1'

    implementation "androidx.annotation:annotation:1.9.1"
    implementation 'androidx.core:core:1.17.0'

    implementation project(':aidl')
    implementation project(':shared')
    implementation project(':api')
    implementation project(':rish')
    implementation project(':server-shared')

    implementation libs.hidden.compat
    compileOnly libs.hidden.stub
}

def outDir = file("$rootDir/out")

afterEvaluate {
    android.applicationVariants.all { variant ->
        def variantCapped = variant.name.capitalize()
        def flavorName = variant.productFlavors[0].name
        def buildTypeLowered = variant.getBuildType().getName().toLowerCase()
        def zipName = "sui-${moduleVersion}-${flavorName}-${buildTypeLowered}.zip"
        def magiskDir = file("$outDir/${flavorName}/${buildTypeLowered}")

        tasks.getByName("pre${variantCapped}Build").dependsOn(":ui:assemble${buildTypeLowered.capitalize()}")

        task("prepareMagiskFiles${variantCapped}", type: Sync) {
            dependsOn("assemble$variantCapped")

            if (flavorName == "standalone") {
                // 现在可以安全地引用了，因为 evaluationDependsOn 保证了任务已注册
                dependsOn project(':standalone').tasks.named("copyStandaloneBinaries")
            }

            def templatePath = "$rootDir/template/magisk_module"
            into magiskDir

            from(templatePath) {
                exclude 'module.prop', 'action.sh', 'post-fs-data.sh', 'customize.sh', 'customize_standalone.sh'
            }

            if (flavorName == "standalone") {
                from(templatePath) { include 'customize_standalone.sh'; rename { 'customize.sh' } }
            } else {
                from(templatePath) { include 'customize.sh' }
            }

            from(templatePath) {
                include 'module.prop'
                expand([
                        id         : (flavorName == "standalone") ? "sui" : moduleId,
                        name       : (flavorName == "standalone") ? "$moduleName (Standalone)" : moduleName,
                        version    : moduleVersion,
                        versionCode: moduleVersionCode.toString(),
                        author     : moduleAuthor,
                        description: (flavorName == "standalone") ? "$moduleDescription [Standalone Mode]" : moduleDescription,
                        updateJson : "https://rikkaapps.github.io/release/sui_zygisk.json",
                ])
                filter(FixCrLfFilter.class, eol: FixCrLfFilter.CrLf.newInstance("lf"))
            }

            from(templatePath) {
                include 'action.sh'
                filter(FixCrLfFilter.class, eol: FixCrLfFilter.CrLf.newInstance("lf"))
            }

            from(templatePath) {
                include 'post-fs-data.sh'
                filter(ReplaceTokens, tokens: [ isStandalone: (flavorName == "standalone").toString() ])
                filter(FixCrLfFilter.class, eol: FixCrLfFilter.CrLf.newInstance("lf"))
            }

            from((buildTypeLowered == "release") ?
                    "$buildDir/intermediates/dex/${variant.name}/minify${variantCapped}WithR8" :
                    "$buildDir/intermediates/dex/${variant.name}/mergeDex$variantCapped") {
                include 'classes.dex'
                rename { 'sui.dex' }
            }

            from("$buildDir/intermediates/stripped_native_libs/${variant.name}/strip${variantCapped}DebugSymbols/out/lib") {
                into 'lib'
            }

            if (flavorName == "standalone") {
                // [修改] 显式指定输出目录路径
                // 因为 copyStandaloneBinaries 现在是普通任务，它把文件放到了 intermediates/standalone_out
                def standaloneOutDir = project(':standalone').layout.buildDirectory.dir("intermediates/standalone_out")

                // 确保依赖关系
                dependsOn project(':standalone').tasks.named("copyStandaloneBinaries")

                // 使用 files() 包装，并用 builtBy 声明构建任务
                from(files(standaloneOutDir) {
                    builtBy project(':standalone').tasks.named("copyStandaloneBinaries")
                }) {
                    into 'standalone_bin'
                }
            }

            from("${rootProject.findProject('ui').buildDir}/outputs/apk/$buildTypeLowered") {
                exclude 'output-metadata.json'
                rename { 'sui.apk' }
            }
            doLast {
                fileTree("$magiskDir").visit { f ->
                    if (f.directory) return
                    if (f.file.name == '.gitattributes') return

                    def md = MessageDigest.getInstance("SHA-256")
                    f.file.eachByte 4096, { bytes, size ->
                        md.update(bytes, 0, size)
                    }
                    file(f.file.path + ".sha256sum").text = md.digest().encodeHex()
                }
            }
        }

        task("zip${variantCapped}", type: Zip) {
            dependsOn("prepareMagiskFiles${variantCapped}")
            from magiskDir
            archiveFileName = zipName
            destinationDirectory = outDir
        }

        task("push${variantCapped}", type: Exec) {
            dependsOn("assemble${variantCapped}")
            workingDir outDir
            commandLine android.adbExecutable, "push", zipName, "/data/local/tmp/"
        }

        task("flash${variantCapped}", type: Exec) {
            dependsOn("push${variantCapped}")
            commandLine android.adbExecutable, "shell", "su", "-c",
                    "magisk --install-module /data/local/tmp/${zipName}"
        }

        task("flashWithKsud${variantCapped}", type: Exec) {
            dependsOn("push${variantCapped}")
            commandLine android.adbExecutable, "shell", "su", "-c",
                    "ksud module install /data/local/tmp/${zipName}"
        }

        task("flashAndReboot${variantCapped}", type: Exec) {
            dependsOn("flash${variantCapped}")
            commandLine android.adbExecutable, "shell", "reboot"
        }

        task("flashWithKsudAndReboot${variantCapped}", type: Exec) {
            dependsOn("flashWithKsud${variantCapped}")
            commandLine android.adbExecutable, "shell", "reboot"
        }
        variant.assembleProvider.get().finalizedBy("zip${variantCapped}")
    }
}
